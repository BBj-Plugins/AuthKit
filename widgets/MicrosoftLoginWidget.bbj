use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::AuthKit/profile/MicrosoftAccountProfile.bbj::MicrosoftAccountProfile
use ::AuthKit/profile/AccountProfile.bbj::AccountProfile

use com.google.api.client.googleapis.auth.oauth2.GoogleIdToken
use com.google.api.client.json.jackson2.JacksonFactory

class public MicrosoftLoginWidget extends BBjWidget

    field private BBjHtmlView HtmlView!
    field private MicrosoftAccountProfile Profile!
    
    field public static BBjNumber ON_LOGIN = 6777
    
    field public static BBjString CLIENTID$ = ""
    field public static BBjString AUTHORITY$ = "https://login.microsoftonline.com/common"
    field public static BBjString REDIRECTURI$ = "http://localhost:8888/files"
    
    field public static BBjNumber AUTOLOGIN = 0
    

    method public MicrosoftLoginWidget(BBjWindow wnd!,BBjInt id!)
        #super!.create(wnd!,id!)
    methodend

    method public MicrosoftLoginWidget(BBjWindow wnd!, BBjInt id!, BBjInt x!, BBjInt y!, BBjInt w!, BBjInt h!)
        #super!.create(wnd!,id!,x!, y!, w!, h!)
    methodend

    rem /**
    rem  * @Override
    rem  * This method is called whenever the widget needs to be rendered
    rem  * @param Boolean f_init!: if TRUE the control is rendered for the first time so this method has to perform initial rendering
    rem  */
    method public void redraw(Boolean f_init!)
        
        wnd! = #getCanvas()
        
        if f_init!>0 then
            ch=unt
            open (ch)"AuthKit/lib/svg/microsoft.svg"
            read record (ch,siz=dec(fin(ch)(1,4)))svg$
            close (ch)
            
            if #H! = null() then
            html$="<html><body><div id='myPMS' style='width:width:100%; height:100%;' onClick='doMicrosoftWidgetLogin();'>"+svg$+"</div><button style='display:none' id='mslogout' onClick='doMicrosoftWidgetLogout();'>logout</button></body></html>"
            else
            html$="<html><body><div id='myPMS' style='width:"+str(#W!-10)+"px; height:"+str(#H!-8)+"px;' onClick='doMicrosoftWidgetLogin();'>"+svg$+"</div><button style='display:none' id='mslogout' onClick='doMicrosoftWidgetLogout();'>logout</button></body></html>"
            fi
            html! = wnd!.addHtmlView(100,0,0,#W!,#H!,html$,$0010$)
            html!.setStyle("position","absolute")
            html!.setStyle("left","0")
            html!.setStyle("right","0")
            html!.setStyle("top","0")            
            

            
            html!.setCallback(BBjAPI.ON_NATIVE_JAVASCRIPT,#this!,"onJs")
            html!.setNoEdge(1)
            url$="https://alcdn.msauth.net/browser/2.35.0/js/msal-browser.min.js"
            script$ = "var link =  $doc.createElement('script');"
            script$ = script$ + "link.setAttribute('type','text/javascript');"
            script$ = script$ + "link.setAttribute('src','"+url$+"');"
            script$ = script$ + "$doc.head.appendChild(link);"
            html!.executeScript(script$)

         html!.setVisible(1)
         bui = info(3,6)="5"
         doc$ = iff(bui,"$doc","document")
         wnd$ = iff(bui,"$wnd","window")


        scr$ = "function whenMSalLoaded (callback) { if (typeof msal === 'undefined') {setTimeout (function () {whenMSalLoaded (callback);}, 100);} else { callback (); }}"
        html!.injectScript(scr$)

        scr$="function doMicrosoftWidgetLogin(){window.msalInstance.loginPopup(window.loginRequest).then(n=>{var t=new CustomEvent(""custom_event"",{bubbles:!0,cancelable:!0});t.myElement=""jsfunction"",t.token=n.accessToken;var e=document.getElementById(""myPMS"");window.basisDispatchCustomEvent(e,t)}).catch(n=>{console.log(n)})}function doMicrosoftWidgetLogout(){msalInstance.logoutPopup()}"
        html!.injectScript(scr$)

        rem minified JS - see AuthKit/lib/js/mslogin.js for source
        js$="whenMSalLoaded(function() {const msalConfig={auth:{clientId:""$$CLIENTID"",authority:""$$AUTHORITY"",redirectUri:""$$REDIRECTURI""},cache:{cacheLocation: 'localStorage',storeAuthStateInCookie: false}};window.loginRequest={scopes:[""user.read""]};window.msalInstance=new msal.PublicClientApplication(msalConfig); if (window.msalInstance.getAllAccounts().length>0) doMicrosoftWidgetLogin();   });"
        
        while pos("$$CLIENTID"=js$)>0
            js$=js$(1,pos("$$CLIENTID"=js$)-1)+#CLIENTID$+js$(pos("$$CLIENTID"=js$)+10)
        wend
        while pos("$$AUTHORITY"=js$)>0
            js$=js$(1,pos("$$AUTHORITY"=js$)-1)+#AUTHORITY$+js$(pos("$$AUTHORITY"=js$)+11)
        wend
        while pos("$$REDIRECTURI"=js$)>0
            js$=js$(1,pos("$$REDIRECTURI"=js$)-1)+#REDIRECTURI$+js$(pos("$$REDIRECTURI"=js$)+13)
        wend

        html!.injectScript(js$,1)
        #HtmlView! = CAST(BBjHtmlView,html!)
        FI

    methodend
    

    method public void onJs( BBjNativeJavaScriptEvent ev! )
        buffer! = ev!.getEventMap() 
        token$=buffer!.get("token")
        #Profile! = new MicrosoftAccountProfile(token$)
        #super!.fireEvent(MicrosoftLoginWidget.ON_LOGIN,#Profile!)
    methodend


    method public AccountProfile getProfile()
        methodret #Profile!
    methodend
    
    method public void logout()
            #HtmlView!.executeScript("$doc.getElementById('mslogout').click();")
            #Profile! = null()
    methodend
    
classend