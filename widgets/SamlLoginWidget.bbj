use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::AuthKit/profile/AccountProfile.bbj::AccountProfile
use ::AuthKit/profile/SamlAccountProfile.bbj::SamlAccountProfile

class public SamlLoginWidget extends BBjWidget

    field private BBjHtmlView HtmlView!
    field public BBjString SamlServletUrl$

    field public static BBjNumber ON_LOGIN = 6777

    field private SamlAccountProfile Profile!

    field private BBjString uuid$ = java.util.UUID.randomUUID().toString()
    field private BBjNamespace ns!

    method public SamlLoginWidget(BBjWindow wnd!,BBjInt id!)
        #super!.create(wnd!,id!)
    methodend

    method public SamlLoginWidget(BBjWindow wnd!, BBjInt id!, BBjInt x!, BBjInt y!, BBjInt w!, BBjInt h!)
        #super!.create(wnd!,id!,x!, y!, w!, h!)
    methodend

    rem /**
    rem  * @Override
    rem  * This method is called whenever the widget needs to be rendered
    rem  * @param Boolean f_init!: if TRUE the control is rendered for the first time so this method has to perform initial rendering
    rem  */
    method public void redraw(Boolean f_init!)

        wnd! = #getCanvas()

        if f_init!>0 then

            #ns! = BBjAPI().getNamespace("SAML.Login","U"+#uuid$,1)
            #ns!.setValue("logindata","")
            #ns!.setCallbackForVariableChange("logindata",#this!,"onLoginData")

            ch=unt
            open (ch)"AuthKit/lib/svg/saml.svg"
            read record (ch,siz=dec(fin(ch)(1,4)))svg$
            close (ch)

            if #H! = null() then
            html$ = "<html><head>            </head><body>           <div style='width:100%; height:100%;' id='SAMLButton' onClick='doLogin();'>"+svg$+"</div>           </body></html>"
            else
            html$ = "<html><body>           <div style='width:"+str(#W!-20)+"px; height:"+str(#H!-28)+"px;' id='SAMLButton' onClick='doLogin();';>"+svg$+"</div>           </body></html>"
            fi

            html! = wnd!.addHtmlView(100,0,0,#W!,#H!,"")
            html!.setStyle("position","absolute")
            html!.setStyle("left","0")
            html!.setStyle("right","0")
            html!.setStyle("top","0")

            html!.setNoEdge(1)
            html!.setCallback(BBjAPI.ON_PAGE_LOADED,#this!,"onPageLoaded")
            html!.setText(html$)
            #HtmlView! = CAST(BBjHtmlView,html!)
            
        FI

    methodend

   method public void onPageLoaded ( BBjPageLoadedEvent ev! )

            x$=#SamlServletUrl$+"doLogin?_t=U"+#uuid$

            bui = info(3,6)>"4"
            doc$ = iff(bui,"$doc","document")
            wnd$ = iff(bui,"$wnd","window")

            scr$="           function doLogin(){                windowObjectReference = window.open(                  """+x$+""",                  ""SAML Login"",                  ""left=20,top=20,width=750,height=550""                );              }"
            #HtmlView!.injectScript(scr$,0)
            #HtmlView!.injectScript(scr$,1)
         wait .2

    methodend

    method public AccountProfile getProfile()
        methodret #Profile!
    methodend

    method public void doLogin()
        #HtmlView!.executeScript("doLogin();")
    methodend

    method public void logout()
            #Profile! = null()
    methodend

    method public void onLoginData(BBjNamespaceEvent ev!)
        attributes! =  ev!.getNewValue()
        #Profile! = new SamlAccountProfile(attributes!)
        #super!.fireEvent(SamlLoginWidget.ON_LOGIN,#Profile!)
    methodend

classend