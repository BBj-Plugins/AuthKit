use ::BBjWidget/BBjWidget.bbj::BBjWidget
use ::AuthKit/profile/GoogleAccountProfile.bbj::GoogleAccountProfile
use ::AuthKit/profile/AccountProfile.bbj::AccountProfile

use com.google.api.client.googleapis.auth.oauth2.GoogleIdToken
use com.google.api.client.json.jackson2.JacksonFactory

class public GoogleLoginWidget extends BBjWidget

    field private BBjHtmlView HtmlView!
    field private GoogleAccountProfile Profile!
    
    field public static BBjNumber ON_LOGIN = 6777
    
    field public static BBjString CLIENTID$ = ""

    method public GoogleLoginWidget(BBjWindow wnd!,BBjInt id!)
        #super!.create(wnd!,id!)
    methodend

    method public GoogleLoginWidget(BBjWindow wnd!, BBjInt id!, BBjInt x!, BBjInt y!, BBjInt w!, BBjInt h!)
        #super!.create(wnd!,id!,x!, y!, w!, h!)
    methodend

    rem /**
    rem  * @Override
    rem  * This method is called whenever the widget needs to be rendered
    rem  * @param Boolean f_init!: if TRUE the control is rendered for the first time so this method has to perform initial rendering
    rem  */
    method public void redraw(Boolean f_init!)
        
        wnd! = #getCanvas()
        
        if f_init!>0 then

        bui = info(3,6)="5"
        doc$ = iff(bui,"$doc","document")
        wnd$ = iff(bui,"$wnd","window")



        html$ = "<html><body>
:     <div id='g_id_onload'
:            data-context='signin'
:            data-client_id='"+#CLIENTID$+"'
:            data-ux_mode='popup'
:            data-callback='myLogin'
:            data-auto_select='true'
:            data-itp_support='true'>
:       </div>
:       
:       <div class='g_id_signin'
:            data-type='standard'
:            data-shape='rectangular'
:            data-theme='outline'
:            data-text='signin_with'
:            data-size='small'
:            data-logo_alignment='left'>
:       </div>"

            html! = wnd!.addHtmlView(100,0,0,#W!,#H!,html$)
            html!.setStyle("position","absolute")
            html!.setStyle("left","0")
            html!.setStyle("right","0")
            html!.setStyle("top","0")
            
            html!.setNoEdge(1)
            html!.setCallback(BBjAPI.ON_NATIVE_JAVASCRIPT,#this!,"onJs")
            #HtmlView! = CAST(BBjHtmlView,html!)

            scr$="function myLogin(response){
:           window.google = google;                
:            var custom=new CustomEvent('custom_event',{bubbles:true,cancelable:true});
:            custom.myElement='jsfunction'; custom.credential = response.credential;
:            var element=document.getElementById('g_id_onload');
:            window.basisDispatchCustomEvent(element,custom);
:            }"        
            #HtmlView!.injectScript(scr$,1)

            script$ = "function revoke(parameter1){google.accounts.id.revoke(parameter1);}"
            #HtmlView!.injectScript(script$,1)

            #HtmlView!.executeScript("var script = "+doc$+".createElement('script');
:           script.type = 'text/javascript';
:           script.src = 'https://accounts.google.com/gsi/client';
:           "+doc$+".head.appendChild(script)")  
        FI
        
    methodend

    method public void onJs( BBjNativeJavaScriptEvent ev! )
          buffer! = ev!.getEventMap() 
          credential$=buffer!.get("credential")
          #Profile! = new GoogleAccountProfile(credential$, #CLIENTID$)
          #super!.fireEvent(GoogleLoginWidget.ON_LOGIN,#Profile!)
    methodend
    
   
    method public AccountProfile getProfile()
        methodret #Profile!
    methodend
    
    method public void logout()
            if #Profile! <> null() then
                #HtmlView!.executeScript("revoke('"+#Profile!.getEMail()+"');")
                #Profile! = null()
            fi
    methodend
    
classend