use ::AuthKit/profile/AccountProfile.bbj::AccountProfile
use java.util.Map

class public SamlAccountProfile extends AccountProfile

    field private Map attributes!

    method private SamlAccountProfile()
    methodend

    method public SamlAccountProfile(Map payload!)
        #attributes! = payload!
        #setFirstName   (#cleanBrackets(str(payload!.get("urn:oid:2.5.4.42"))))
        #setLastName    (#cleanBrackets(str(payload!.get("urn:oid:2.5.4.4"))))
        #setFullName    (#cleanBrackets(str(payload!.get("urn:oid:2.16.840.1.113730.3.1.241"))))
        #setEMail       (#cleanBrackets(str(payload!.get("urn:oid:0.9.2342.19200300.100.1.3"))))
        #setAuthProvider("SAML")
    methodend
    
    method private String cleanBrackets(String in!)
        if in!.startsWith("[") then
            in! = in!.substring(1)
        fi
        if in!.endsWith("]") then
            in! = in!.substring(0,in!.length()-1)
        fi
        methodret in!
    methodend
    
    method public Map getSamlAttributes()
        methodret #attributes!
    methodend

    
classend