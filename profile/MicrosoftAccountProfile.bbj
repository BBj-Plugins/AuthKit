use ::AuthKit/profile/AccountProfile.bbj::AccountProfile
use com.google.gson.JsonParser
use java.io.BufferedReader
use java.io.InputStreamReader
use java.net.HttpURLConnection
use java.net.URL
use java.util.Base64
use org.apache.commons.io.FileUtils
use java.io.File

class public MicrosoftAccountProfile extends AccountProfile

    field private BBjString Token$

    method private MicrosoftAccountProfile()
    methodend

    method public MicrosoftAccountProfile(BBjString token!)
        #buildWithAccessToken(token!)
        #Token$ = token!
    methodend

    method public BBjString getToken()
        methodret #Token$
    methodend

    method private void buildWithAccessToken(BBjString token!)

        url! = new URL("https://graph.microsoft.com/v1.0/me/")
        conn! = url!.openConnection()
        conn!.setRequestMethod("GET")
        conn!.setRequestProperty("Authorization", "Bearer " + str(token!))
        conn!.setRequestProperty("Accept", "application/json")
        br! = new BufferedReader(new InputStreamReader(conn!.getInputStream()))
        loop:
        output! = br!.readLine() 
        if output! <> null () then
                x$=x$+output!
                goto loop
        fi
        conn!.disconnect()      
            
        jobj! = new JsonParser().parse(x$)
        
        #AuthProvider!  = "Microsoft"
        #FirstName!     = #trimQuotes(str(jobj!.get("givenName")))
        #LastName!      = #trimQuotes(str(jobj!.get("surname")))
        #FullName!      = #trimQuotes(str(jobj!.get("displayName")))
        #EMail!         = #trimQuotes(str(jobj!.get("userPrincipalName")))
        if jobj!.get("prferredLanguage") <> null() then
            #Locale!        = #trimQuotes(str(jobj!.get("prferredLanguage")))
        fi
        
        x$=""
        url! = new URL("https://graph.microsoft.com/v1.0/me/photo/$value")
        conn! = url!.openConnection()
        conn!.setRequestMethod("GET")
        conn!.setRequestProperty("Authorization", "Bearer " + str(token!))
        conn!.setRequestProperty("Accept", "*/*")
        
        
        havePic=0
        
        is! = conn!.getInputStream(err=nophoto)
        buffer! = new byte[4096]
        sb! = new java.lang.StringBuilder()
        
        loop1:
        n = is!.read(buffer!)
        if n>-1 then
            sb!.append(buffer!,0,n)
            goto loop1
        fi
        
        havePic=1
        
        nophoto:
        conn!.disconnect(err=*next)   
        
        if havePic=0 then
            #PictureUrl! = null()
        else
            x! = sb!.toString()
            
            rem todo: figure out how to do this without an intermediate file, so that the B64 string 
            rem does not produce garbled output
            
            tmpf! = File.createTempFile("pic-","jpg")
            tmpf!.deleteOnExit()
            p$ = tmpf!.getAbsolutePath()
            tmpf!.delete() 
            
            ch=unt
            open (ch,mode="O_CREATE,O_TRUNC")p$
            write (ch)x!
            close (ch)

            fileContent! = FileUtils.readFileToByteArray(tmpf!)
            tmpf!.delete()
            
            encodedString! = Base64.getEncoder().encodeToString(fileContent!)
            #PictureUrl!    = "data:image/jpeg;base64,"+encodedString!
        fi
    methodend
    
    method private BBjString trimQuotes(BBjString txt!)
        if txt!.startsWith("""") then
            txt! = str(txt!)(2)
        fi
        if txt!.endsWith("""") then
            txt! = str(txt!)(1,len(txt!)-1)
        fi
        methodret txt!
    methodend
classend